/*
 * Copyright Â© 2012 Keith Packard <keithp@keithp.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 2 of the License.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA.
 */

autoload Client;
autoload Nichrome;
autoload Nichrome::Box;
autoload Nichrome::Label;
autoload Skiplist;

extend namespace Client {
	public namespace Userlist {

		import Nichrome;

		public typedef struct {
			string		name;
			*Label::label_t	name_label;
			bool		playing;
			int		score;
			*Label::label_t	score_label;
			int		bid;
			*Label::label_t	bid_label;
			string		mode;
			*Label::label_t	mode_label;
		} user_t;

		public typedef union {
			*user_t		user;
			void		none;
		} user_or_none_t;

		public typedef struct {
			*nichrome_t		ui;
			*Box::box_t		hbox;
			*Label::label_t		name_label;
			*Label::label_t		score_label;
			*Label::label_t		bid_label;
			*Label::label_t		mode_label;
			*Box::box_t		name_box;
			*Box::box_t		bid_box;
			*Box::box_t		score_box;
			*Box::box_t		mode_box;
			Skiplist::Skip		names;
			int			count;
			user_or_none_t		showing;
			string			font;
			string			bold_font;
			string			italic_font;
		} userlist_t;

		bool user_greater(*user_t a, *user_t b)  = a->name > b->name;

		protected void set_boxes(&userlist_t ul) {
			ul.name_box->items = (Box::item_t[...]) {};
			ul.score_box->items = (Box::item_t[...]) {};
			ul.bid_box->items = (Box::item_t[...]) {};
			ul.mode_box->items = (Box::item_t[...]) {};
			Box::add_item(ul.name_box, Box::widget_item(ul.name_label, 1, 0));
			Box::add_item(ul.score_box, Box::widget_item(ul.score_label, 1, 0));
			Box::add_item(ul.bid_box, Box::widget_item(ul.bid_label, 1, 0));
			Box::add_item(ul.mode_box, Box::widget_item(ul.mode_label, 1, 0));
			Skiplist::walk (ul.names, void func (*user_t user) {
					Box::add_item(ul.name_box, Box::widget_item(user->name_label, 0, 0));
					Box::add_item(ul.score_box, Box::widget_item(user->score_label, 0, 0));
					Box::add_item(ul.bid_box, Box::widget_item(user->bid_label, 0, 0));
					Box::add_item(ul.mode_box, Box::widget_item(user->mode_label, 0, 0));
				});
			Box::add_item(ul.name_box, Box::glue_item(0, 1));
			Box::add_item(ul.score_box, Box::glue_item(0, 1));
			Box::add_item(ul.bid_box, Box::glue_item(0, 1));
			Box::add_item(ul.mode_box, Box::glue_item(0, 1));
		}

		string score_string(bool playing, int score) {
			if (playing)
				return sprintf("%d", score);
			return "";
		}

		string bid_string(bool playing, int bid) {
			if (playing)
				return sprintf("%d", bid);
			return "";
		}

		public void score(*userlist_t ul, string name, int score) {
			*user_t find = &(user_t) {
				.name = name
			};
			try {
				*user_t user = Skiplist::search(ul->names, find);
				user->score = score;
				Label::relabel(user->score_label, score_string(user->playing, user->score));
			} catch Skiplist::not_found(*user_t x) {
			}
		}

		public void bid(*userlist_t ul, string name, int bid) {
			try {
				*user_t user = Skiplist::search(ul->names,
								&(user_t) { .name = name });
				user->bid = bid;
				Label::relabel(user->bid_label, bid_string(user->playing, user->bid));
			} catch Skiplist::not_found(*user_t x) {
			}
		}

		public void clear_bid(*userlist_t ul) {
			Skiplist::walk (ul->names, void func (*user_t user) {
					user->bid = 0;
					Label::relabel(user->bid_label, bid_string(user->playing, user->bid));
				});
		}

		void set_showing (*userlist_t ul, *user_t user, bool showing) {
			if (showing) {
				if (ul->showing != (user_or_none_t.user) user) {
					ul->showing = (user_or_none_t.user) user;
					user->name_label->font = ul->bold_font;
				}
			} else {
				if (ul->showing == (user_or_none_t.user) user) {
					ul->showing = user_or_none_t.none;
					user->name_label->font = ul->font;
				}
			}
		}

		public void showing(*userlist_t ul, string name, bool showing) {
			try {
				*user_t user = Skiplist::search(ul->names,
								&(user_t) { .name = name });
				set_showing (ul, user, showing);
			} catch Skiplist::not_found(*user_t x) {
			}
		}

		public void clear_showing(*userlist_t ul) {
			union switch (ul->showing) {
			case user u:
				set_showing (ul, u, false);
				break;
			default:
			}
		}

		public void clear_modes(*userlist_t ul) {
			Skiplist::walk (ul->names, void func (*user_t user) {
					Label::relabel(user->mode_label, "");
				});
		}

		public void set_mode(*userlist_t ul, string name, string mode) {
			try {
				*user_t user = Skiplist::search(ul->names,
								&(user_t) { .name = name });
				user->mode = mode;
				Label::relabel(user->mode_label, user->mode);
			} catch Skiplist::not_found(*user_t x) {
			}
		}

		public void add(*userlist_t ul, string name, bool playing, int score, int bid) {
			*user_t find = &(user_t) { .name = name };
			try {
				Skiplist::search(ul->names, find);
			} catch Skiplist::not_found(*user_t x) {
				*user_t user = &(user_t) {
					.name = name,
					.name_label = Label::new(ul->ui, name),
					.playing = playing,
					.score = score,
					.score_label = Label::new(ul->ui, score_string(playing, score)),
					.bid = bid,
					.bid_label = Label::new(ul->ui, bid_string(playing, bid)),
					.mode = "",
					.mode_label = Label::new(ul->ui, ""),
				};
				if (!playing)
					user->name_label->font = ul->italic_font;
				Skiplist::insert(ul->names, user);
				ul->count = Skiplist::length(ul->names);
				set_boxes(ul);
			}
		}

		public void remove(*userlist_t ul, string name) {
			*user_t find = &(user_t) { .name = name };
			try {
				*user_t user = Skiplist::search(ul->names, find);
				Skiplist::delete(ul->names, user);
				Nichrome::remove(ul->ui, user->name_label);
				Nichrome::remove(ul->ui, user->score_label);
				ul->count = Skiplist::length(ul->names);
				set_boxes(ul);
			} catch Skiplist::not_found(string x) {
			}
		}

		protected void init (*userlist_t ul,
				     *nichrome_t ui) {
			ul->ui = ui;
			ul->font = Widget::default_font;
			ul->bold_font = sprintf("%s:bold", Widget::default_font);
			ul->italic_font = sprintf("%s:italic", Widget::default_font);

			ul->names = Skiplist::new(user_greater);
			ul->name_label = Label::new(ui, "Player");
			ul->name_label->font = ul->bold_font;
			ul->score_label = Label::new(ui, "Score");
			ul->score_label->font = ul->bold_font;
			ul->bid_label = Label::new(ui, "Bid");
			ul->bid_label->font = ul->bold_font;
			ul->mode_label = Label::new(ui, "State");
			ul->mode_label->font = ul->bold_font;
			ul->name_box = Box::new(Box::dir_t.vertical);
			ul->score_box = Box::new(Box::dir_t.vertical);
			ul->bid_box = Box::new(Box::dir_t.vertical);
			ul->mode_box = Box::new(Box::dir_t.vertical);
			ul->hbox = Box::new(Box::dir_t.horizontal,
					    Box::box_item(ul->name_box),
					    Box::box_item(ul->score_box),
					    Box::box_item(ul->bid_box),
					    Box::box_item(ul->mode_box));
			ul->showing = user_or_none_t.none;
			ul->count = 0;
		}

		public *userlist_t new (*nichrome_t ui) {
			*userlist_t	ul = &(userlist_t) {};
			init(ul, ui);
			return ul;
		}
	}
}
